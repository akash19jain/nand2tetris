// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not(in=instruction[15], out=inA);
    Not(in=inA, out=inC);
    
    And(a=inC, b=instruction[5], out=toA);    // C-inst and dest to A-reg?
    Mux16(a=instruction, b=ALUout, sel=toA, out=Ain);
    
    Or(a=inA, b=toA, out=loadA);    // load A if A-inst or C-inst&dest to A-reg
    ARegister(in=Ain, load=loadA, out=Aout);
    
    Mux16(a=Aout, b=inM, sel=instruction[12], out=outAM);   // select A or M based on a-bit

    And(a=inC, b=instruction[4], out=loadD);
    DRegister(in=ALUout, load=loadD, out=outD);    // load the D register from ALU
    
    ALU(x=outD, y=outAM, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7],
        no=instruction[6], out=ALUout, zr=outZR, ng=outNG); // calculate
        
    // Set outputs for writing memory
    Or16(a=false, b=Aout, out[0..14]=addressM);
    Or16(a=false, b=ALUout, out=outM);

    And(a=inC, b=instruction[3], out=writeM);
    
    // calc loadPC & PCinc - whether to load PC with A reg
    And(a=outZR, b=instruction[1], out=JQ);    // is zero and jump if zero
    
    And(a=outNG, b=instruction[2], out=JT);    // is neg and jump if neg
    Or(a=outZR, b=outNG, out=LEZ);
    
    Not(in=LEZ, out=positive);            // is positive (not zero and not neg)
    
    And(a=positive, b=instruction[0], out=jt); // is pos and jump if pos
    Or(a=JQ, b=JT, out=JLE);
    Or(a=JLE, b=jt, out=Ajump);              // load PC if cond met and jump if cond
   
    And(a=inC, b=Ajump, out=loadPC); // Only jump if C instruction
    
    Not(in=loadPC, out=PCinc);                  // only inc if not load
    PC(in=Aout, inc=PCinc, load=loadPC, reset=reset, out[0..14]=pc);
}